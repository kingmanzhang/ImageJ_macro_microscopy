// This macro batch measures a folder of images.
// need to set Brightness&Contrast before runing this macro.
// need to change rolling cycle size and threshold for a particular cell clone
// default values are used for BON A9. 
// This macro is edited by Aaron in Martin's Lab. 

macro "Batch Measure" {
    dir = getDirectory("Choose a Directory ");
    list = getFileList(dir);
    if (getVersion>="1.40e")
        setOption("display labels", true);
    run("Set Measurements...", "area mean integrated limit display redirect=None decimal=3");
    setBatchMode(true);
    for (i=0; i<list.length; i++) {
        path = dir+list[i];
        showProgress(i, list.length);
        if (!endsWith(path,"/")) open(path);
        if (nImages>=1) {
            run("Subtract Background...", "rolling=12");
            setAutoThreshold("Default");
            //run("Threshold...");
            setThreshold(10, 800);
            run("Measure");
            close();
        }
    }
}

